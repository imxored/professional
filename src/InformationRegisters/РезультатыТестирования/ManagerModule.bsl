
Функция ПолучитьИдентификаторНовойСессии() Экспорт

	Результат = Новый УникальныйИдентификатор;
	
	Набор = РегистрыСведений.РезультатыТестирования.СоздатьНаборЗаписей();
	Набор.Отбор.ИдентификаторПопытки.Установить(Результат);
	
	Для Каждого Вопрос Из ТестированиеСлужебный.ПодобратьВопросыДляТестирования() Цикл
		Запись = Набор.Добавить();
		Запись.ИдентификаторПопытки = Результат;
		Запись.Вопрос = Вопрос;
		Запись.ВремяНачала = ТекущаяДатаСеанса();
	КонецЦикла;
	
	Набор.Записать();
	
	Возврат Результат;
	
КонецФункции

Процедура ЗафиксироватьОтвет(Идентификатор, Вопрос, Ответ) Экспорт
	
	Запись = РегистрыСведений.РезультатыТестирования.СоздатьМенеджерЗаписи();
	Запись.ИдентификаторПопытки = Идентификатор;
	Запись.Вопрос = Вопрос;
	
	Запись.Прочитать();
	
	Если Не Запись.Выбран() Тогда
		Запись.ИдентификаторПопытки = Идентификатор;
		Запись.Вопрос = Вопрос;
		Запись.ВремяНачала = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Запись.Ответ = Ответ;
	Запись.ВремяОкончания = ТекущаяДатаСеанса();
	Запись.Результат = Ответ.Верный;
	Запись.Записать();
	
КонецПроцедуры